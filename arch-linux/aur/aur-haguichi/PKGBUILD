# Maintainer: Stephen Brandt <stephen@stephenbrandt.com>
# Contributor: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG

pkgname=haguichi
pkgver=1.4.1
pkgrel=1
pkgdesc="Provides a user friendly GUI to control the Hamachi client on Linux"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
url="https://www.haguichi.net"
license=('GPL3')
depends=('gtk3' 'libappindicator-gtk3' 'libnotify' 'logmein-hamachi')
makedepends=('meson' 'vala')
source=("http://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.xz")
sha256sums=('4ace875a5db85b3df17ac733de67f64213c82ac41af287e3cb9bc33c4a342af7')

prepare() {
  cd $pkgname-$pkgver

  sed -i '/OnlyShowIn/s/$/LXDE;/' data/com.github.ztefn.haguichi.autostart.desktop.in.in
  sed -i '$a X-LXDE-Autostart-enabled=false' data/com.github.ztefn.haguichi.autostart.desktop.in.in
  sed -i 's/false/true/g' data/com.github.ztefn.haguichi.autostart.desktop.in.in
  sed -i '/Terminal/s/true/false/' data/com.github.ztefn.haguichi.autostart.desktop.in.in

  rm -rf build
  mkdir build
}

build() {
  cd $pkgname-$pkgver/build

  meson -Denable-appindicator=true ..
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C $pkgname-$pkgver/build install
}
